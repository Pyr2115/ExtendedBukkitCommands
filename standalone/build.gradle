plugins {
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT"
    implementation project(":common")
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            publication.artifact javadocJar
            publication.artifact sourcesJar

            artifactId = "director-standalone"

            pom {
                name.set("director-standalone")
                description.set("Extension of the Bukkit command framework")
                url.set("https://github.com/Pyrbu/Director")
            }
        }
    }
    repositories {
        maven {
            if (parent.properties.containsKey("DIST_USERNAME") && parent.properties.containsKey("DIST_PASSWORD")) {
                credentials {
                    username parent.properties.get("DIST_USERNAME")
                    password parent.properties.get("DIST_PASSWORD")
                }
            }
            url = uri("https://repo.pyr.lol/releases/")
        }
    }
}