plugins {
    id "java-library"
    id "maven-publish"
    id "io.freefair.lombok" version "6.5.1"
}

repositories {
    mavenCentral()
    maven {
        url "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.19.3-R0.1-SNAPSHOT"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.2"
}

test {
    useJUnitPlatform()
}

group = "lol.pyr"
version = "1.6.2"

compileJava {
    options.release.set(17)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar

            pom {
                name.set("extendedcommands")
                description.set("Extension of the Bukkit command framework")
                url.set("https://github.com/Pyr2115/ExtendedBukkitCommands")
            }
        }
    }
    repositories {
        maven {
            credentials {
                username DIST_USERNAME
                password DIST_PASSWORD
            }
            url = uri("https://repo.pyr.lol/releases/")
        }
    }
}