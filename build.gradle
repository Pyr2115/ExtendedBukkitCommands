subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.2"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.2"
    }

    test {
        useJUnitPlatform()
    }

    compileJava {
        options.release.set(8)
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name.set("extendedcommands-" + project.name)
                    description.set("Extension of the Bukkit command framework")
                    url.set("https://github.com/Pyrbu/ExtendedBukkitCommands")
                }
            }
        }
        repositories {
            maven {
                if (properties.containsKey("DIST_USERNAME") && properties.containsKey("DIST_PASSWORD")) {
                    credentials {
                        username properties.get("DIST_USERNAME")
                        password properties.get("DIST_PASSWORD")
                    }
                }
                url = uri("https://repo.pyr.lol/releases/")
            }
        }
    }

    group = "lol.pyr"
    version = "2.0.0"

    repositories {
        mavenCentral()
        maven {
            url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            url "https://repo.papermc.io/repository/maven-public/"
        }
    }
}

