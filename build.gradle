subprojects {
    apply plugin: "java-library"


    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.2"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.2"
    }

    test {
        useJUnitPlatform()
    }

    compileJava {
        options.release.set(8)
    }

    if (!project.name.equalsIgnoreCase("common")) {
        apply plugin: "maven-publish"

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    groupId = "lol.pyr"
                    artifactId = "director-" + project.name

                    from components.java
                    pom {
                        name.set("director-" + project.name)
                        description.set("Extension of the Bukkit command framework")
                        url.set("https://github.com/Pyrbu/Director")
                    }
                }
            }
            repositories {
                maven {
                    if (parent.properties.containsKey("DIST_USERNAME") && parent.properties.containsKey("DIST_PASSWORD")) {
                        credentials {
                            username parent.properties.get("DIST_USERNAME")
                            password parent.properties.get("DIST_PASSWORD")
                        }
                    }
                    url = uri("https://repo.pyr.lol/releases/")
                }
            }
        }
    }


    group = "lol.pyr"
    version = "2.0.0"

    repositories {
        mavenCentral()
        maven {
            url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            url "https://repo.papermc.io/repository/maven-public/"
        }
    }
}

